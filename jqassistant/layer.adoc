[[layer:Default]]
[role=group,includesConstraints="layer:Cycles, layer:ForbiddenRelations", includesConcepts="layer:LayerDependency.graphml, layer:WantedRelations, layer:ExistingRelations"]
== Layer Rules

All packages which are directly contained in the root package are technical layers.

[[layer:Layer]]
.Adds the label `Layer` to all packages contained in the root package (of all non-test artifacts).
[source,cypher,role=concept,requiresConcepts="structure:RootPackage"]
----
MATCH
  (artifact:Artifact)-[:CONTAINS]->(root:Package:Root)-[:CONTAINS]->(layer:Package)
WHERE
   artifact.type <> "test-jar"
SET
  layer:Layer
RETURN
  layer as Layer
----

[[layer:ExistingRelations]]
.Adds the relation `LAYER_USE` for all Layers which contains classes which depends on each other. Add the information `allowed`, `count` (nr of dependencies), `dependencies`.
[source,cypher,role=concept,requiresConcepts="layer:Layer"]
----
MATCH
    (L1:Layer)-[:CONTAINS*]->(t1:Type),
    (L2:Layer)-[:CONTAINS*]->(t2:Type),
    (t1)-[:DEPENDS_ON]->(t2)
WHERE
    L1.fqn <> L2.fqn
WITH L1,L2,count(*) as times, collect(distinct t1.fqn + "->" + t2.fqn) as dependencies
MERGE (L1)-[lu:LAYER_USE]->(L2)
ON CREATE SET lu.allowed = FALSE
SET lu.count = times
SET lu.dependencies=dependencies
RETURN L1, L2
----

[[layer:WantedRelations]]
.Adds wanted relation `LAYER_USE` to all Layers with information `allowed` to `true`.
[source,cypher,role=concept,requiresConcepts="layer:Layer"]
----
MATCH
 (config:Layer{name:"config"}),
 (constraints:Layer{name:"constraints"}),
 (dao:Layer{name:"dao"}),
 (frontend:Layer{name:"frontend"}),
 (jobs:Layer{name:"jobs"}),
 (model:Layer{name:"model"}),
 (security:Layer{name:"security"}),
 (service:Layer{name:"service"}),
 (util:Layer{name:"util"})
MERGE
 (constraints)-[cu:LAYER_USE{allowed:true}]->(util)
MERGE
 (config)-[:LAYER_USE{allowed:true}]->(security)
MERGE
 (config)-[:LAYER_USE{allowed:true}]->(frontend)
MERGE
 (jobs)-[:LAYER_USE{allowed:true}]->(service)
MERGE
 (dao)-[:LAYER_USE{allowed:true}]->(model)
MERGE
 (model)-[:LAYER_USE{allowed:true}]->(util)
MERGE
 (model)-[:LAYER_USE{allowed:true}]->(constraints)
MERGE
 (frontend)-[:LAYER_USE{allowed:true}]->(service)
MERGE
 (frontend)-[:LAYER_USE{allowed:true}]->(model)
MERGE
 (frontend)-[:LAYER_USE{allowed:true}]->(util)
MERGE
 (service)-[:LAYER_USE{allowed:true}]->(dao)
MERGE
 (service)-[:LAYER_USE{allowed:true}]->(model)
MERGE
 (service)-[:LAYER_USE{allowed:true}]->(util)
RETURN
 *
----


[[layer:LayerDependency.graphml]]
.Creates a GraphML report about layers and their dependencies.
[source,cypher,role=concept,requiresConcepts="layer:Layer"]
----
MATCH
  (user:Layer)-[use:LAYER_USE]->(used:Layer)
RETURN
  user as User, used as Used, {
    role :     "relationship",
    type :     "USE",
    startNode: user,
    endNode:   used,
    properties: {
      weight: SIZE(use.dependencies)
    }
  } as Dependency
----


[[layer:ForbiddenRelations]]
.Find all unwanted relations.
[source,cypher,role=constraint,requiresConcepts="layer:ExistingRelations, layer:WantedRelations"]
----
MATCH
  (from:Layer)-[rel:LAYER_USE{allowed:false}]->(to:Layer)
RETURN
  from.name AS From, to.name AS To, REDUCE( tr ="", dep in rel.dependencies[0..5] | tr + dep + "\n") AS ForbiddenDependenciesMax5
----

Cyclic dependencies between layer must be avoided to keep the possibility of later refactorings and restructurings.

[[layer:Cycles]]
.There must be no cyclic dependencies between layers.
[source,cypher,role=constraint,requiresConcepts="layer:ExistingRelations"]
----
MATCH
  (L1:Layer)-[sr:LAYER_USE]->(L2:Layer),
  cycle=SHORTESTPATH((L2)-[:LAYER_USE*]->(L1))
RETURN
  REDUCE(result = L1.name, rel in sr + RELATIONSHIPS(cycle)|
    result + " - " + rel.count + ", [" +
    // Max 5 Dependencies are enough
    REDUCE( tr ="", dep in rel.dependencies[0..5] | tr + dep + "\n") +
    "] -> " + endNode(rel).name)
  AS Cycle
----

