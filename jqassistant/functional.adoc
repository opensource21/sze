[[functional:Default]]
[role=group,includesConstraints="functional:Cycles, functional:ForbiddenRelations", includesConcepts="functional:Slice, functional:SliceDependency.graphml, functional:ExistingRelations"]
== Functional Slices Rules

The program is splittet into the following slices
- "admin"
- "konfiguration"
- "document"
- "crud"
- "zeugnis",
- "zeugnisconfig"
- "stammdaten"
- "calendar"
- "base"
Which could be referenced from top to bottom.

[[functional:Slice]]
.Create the node `Slice`.
[source,cypher,role=concept,requiresConcepts="structure:RootPackage"]
----
MATCH (artifact:Artifact)
WHERE
   artifact.type <> "test-jar"
MATCH
  (artifact:Artifact)-[:CONTAINS]->(root:Package{fqn:"net.sf.sze.dao.api"})-[:CONTAINS]->(slice:Package)
WITH COLLECT(slice) AS rows1
MATCH
  (artifact:Artifact)-[:CONTAINS]->(root:Package{fqn:"net.sf.sze.frontend"})-[:CONTAINS]->(slice:Package)
WITH rows1 + COLLECT(slice) AS rows2
MATCH
  (artifact:Artifact)-[:CONTAINS]->(root:Package{fqn:"net.sf.sze.service"})-[:CONTAINS *2..2]->(slice:Package)
WITH rows2 + COLLECT(slice) AS rows3
UNWIND rows3 AS slice
MERGE (sn:Artifact:Slice{name:slice.name, fqn:slice.name})
MERGE (sn)-[:SLICE_CONTAINS]-> (slice)
RETURN
  sn
----

[[functional:ExistingRelations]]
.Adds the relation `SLICE_USE` for all Slices which contains classes which depends on each other. Add the information `allowed`, `count` (nr of dependencies), `dependencies`.
[source,cypher,role=concept,requiresConcepts="functional:WantedRelations, maven:InternalFile"]
----
MATCH
    (L1:Slice)-[:SLICE_CONTAINS]->(:Package:Internal)-[:CONTAINS*]->(t1:Type:Internal),
    (L2:Slice)-[:SLICE_CONTAINS]->(:Package:Internal)-[:CONTAINS*]->(t2:Type:Internal),
    (t1)-[:DEPENDS_ON]->(t2)
WHERE L1.name <> L2.name
WITH L1,L2,count(*) as times, collect(distinct t1.fqn + "->" + t2.fqn) as dependencies
MERGE (L1)-[su:SLICE_USE]->(L2)
ON CREATE SET su.allowed = FALSE
SET su.count = times
SET su.dependencies=dependencies
RETURN L1, L2
----

[[functional:WantedRelations]]
.Adds wanted relation `SLICE_USE` to all Slicees with information `allowed` to `true`.
[source,cypher,role=concept,requiresConcepts="functional:Slice"]
----
MATCH
    (admin:Slice{name:"admin"}),
    (konfiguration:Slice{name:"konfiguration"}),
    (document:Slice{name:"document"}),
    (crud:Slice{name:"crud"}),
    (zeugnis:Slice{name:"zeugnis"}),
    (zeugnisconfig:Slice{name:"zeugnisconfig"}),
    (stammdaten:Slice{name:"stammdaten"}),
    (use:Slice),
    (calendar:Slice{name:"calendar"}),
    (base:Slice{name:"base"})
WHERE use.name <> "calendar" AND use.name <> "base"
MERGE
  (admin)-[:SLICE_USE{allowed:true}]->(zeugnis)
MERGE
  (admin)-[:SLICE_USE{allowed:true}]->(stammdaten)
MERGE
  (konfiguration)-[:SLICE_USE{allowed:true}]->(zeugnis)
MERGE
  (konfiguration)-[:SLICE_USE{allowed:true}]->(zeugnisconfig)
MERGE
  (konfiguration)-[:SLICE_USE{allowed:true}]->(stammdaten)
MERGE
  (crud)-[:SLICE_USE{allowed:true}]->(zeugnis)
MERGE
  (document)-[:SLICE_USE{allowed:true}]->(zeugnis)
MERGE
  (document)-[:SLICE_USE{allowed:true}]->(zeugnisconfig)
MERGE
  (zeugnis)-[:SLICE_USE{allowed:true}]->(zeugnisconfig)
MERGE
  (zeugnis)-[:SLICE_USE{allowed:true}]->(stammdaten)
MERGE
  (use)-[:SLICE_USE{allowed:true}]->(calendar)
MERGE
  (use)-[:SLICE_USE{allowed:true}]->(base)
RETURN
 *

----


[[functional:ForbiddenRelations]]
.Find all unwanted relations.
[source,cypher,role=constraint,requiresConcepts="functional:ExistingRelations, functional:WantedRelations"]
----
MATCH
  (from:Slice)-[rel:SLICE_USE{allowed:false}]->(to:Slice)
RETURN
  from.name AS From, to.name AS To, REDUCE( tr ="", dep in rel.dependencies[0..5] | tr + dep + "\n") AS ForbiddenDependenciesMax5
----

Cyclic dependencies between slices must be avoided to keep the possibility of later refactorings and restructurings.

[[functional:Cycles]]
.There must be no cyclic dependencies between slices.
[source,cypher,role=constraint,requiresConcepts="functional:ExistingRelations"]
----
MATCH
  (L1:Slice)-[sr:SLICE_USE]->(L2:Slice),
  cycle=SHORTESTPATH((L2)-[:SLICE_USE*]->(L1))
RETURN
  REDUCE(result = L1.name, rel in sr + RELATIONSHIPS(cycle)|
    result + " - " + rel.count + ", [" +
    // Max 5 Dependencies are enough
    REDUCE( tr ="", dep in rel.dependencies[0..5] | tr + dep + "\n") +
    "] -> " + endNode(rel).name)
  AS Cycle
----

[[functional:SliceDependency.graphml]]
.Creates a GraphML report about slices and their dependencies.
[source,cypher,role=concept,requiresConcepts="functional:Slice"]
----
MATCH
  (slice1:Slice)-[:SLICE_CONTAINS]->()-[:CONTAINS*]->(t1:Type),
  (slice2:Slice)-[:SLICE_CONTAINS]->()-[:CONTAINS*]->(t2:Type),
  (t1)-[d:DEPENDS_ON]->(t2)
WHERE
   slice1 <> slice2
WITH
  slice1, slice2, count(d) as weight
RETURN
  slice1 as Slice1, slice2 as Slice2, {
    role :     "relationship",
    type :     "DEPENDS_ON",
    startNode: slice1,
    endNode:   slice2,
    properties: {
      weight: weight
    }
  } as Dependency
----

